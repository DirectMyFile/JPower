apply plugin: "build-dashboard"
apply plugin: "project-report"

group = "com.directmyfile.jpower"
version = "1.3.0"

subprojects {
    apply plugin: "java"

    version = rootProject.version

    repositories {
        mavenCentral()

        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    dependencies {
        testCompile "junit:junit:4.11"
    }

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

task jarAll(type: Jar) {
    destinationDir = file("${buildDir}/libs/")
    classifier = "${project.name}-${project.version}-all"
    childProjects.values().each { child ->
        dependsOn.add(child.tasks.getByName("classes"))
        dependsOn.add(child.tasks.getByName("processResources"))
        from "${child.buildDir}/classes/main"
        from "${child.buildDir}/resources/main"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.11"
}

apply from: 'gradle/pmd.gradle'
apply from: 'gradle/version.gradle'
apply from: 'gradle/upload.gradle'
