import jpower.gradle.*

apply plugin: "build-dashboard"
apply plugin: "project-report"

group = "com.directmyfile.jpower"
version = getProperty("jpower.version")

subprojects {
    apply plugin: "java"

    version = rootProject.version

    JPower.setProject(project)
    JPower.setup()

    repositories {
        mavenCentral()

        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }

    dependencies {
        compile "com.intellij:annotations:12.0"
        testCompile "junit:junit:4.11"
    }

    compileJava {
        options.warnings = false
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

task jarAll(type: JarAllTask) {
    group = "build"
    description = "Builds a Jar with all JPower Modules"
}

task wrapper(type: Wrapper) {
    group = "build setup"
    description = "Builds/Updates the Gradle Wrapper"
    gradleVersion = "2.0-20140322230012+0000"
}

task release(type: ReleaseTask) {
    group = "publishing"
    description = "Releases JPower as configured"
    git(tag: true, push: false)
}

tasks.getByName("buildDashboard").with {
    group = "reporting"
    description = "Creates a project report site"
}

apply from: 'gradle/pmd.gradle'
