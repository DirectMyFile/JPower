apply plugin: "java"
apply plugin: "maven"
apply plugin: "signing"

group = rootProject.group
version = rootProject.version
description = "JPower ${project.name} Module"

repositories {
    mavenCentral()
    maven {
        name = "snapshots"
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile "org.jetbrains:annotations:13.0"
    testCompile "junit:junit:4.12"
}

compileJava {
    options.warnings = false
    options.compilerArgs << '-XDignore.symbol.file'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

test {
    maxParallelForks = 2 // Running Tests in Parallel really helps the build
    useJUnit() // Ensure we use JUnit
}

jar {
    manifest {
        attributes "Implementation-Title": "JPower ${project.name} Module", "Implementation-Version": project.version
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            configuration = configurations.archives

            repository(url: "http://nexus.directcode.org/nexus/content/repositories/releases/") {
                authentication(userName: project.property("dcUsername"), password: project.property("dcPassword"))
            }

            snapshotRepository(url: "http://nexus.directcode.org/nexus/content/repositories/snapshots/") {
                authentication(userName: project.property("dcUsername"), password: project.property("dcPassword"))
            }

            pom {
                groupId = rootProject.group
                version = project.version
                artifactId = project.archivesBaseName

                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description "JPower ${project.name} Module"
                    url 'http://github.com/DirectMyFile/JPower/'

                    scm {
                        url 'https://github.com/DirectMyFile/JPower'
                        connection 'scm:git:git://github.com/DirectMyFile/JPower.git'
                        developerConnection 'scm:git:git@github.com:DirectMyFile/JPower.git'
                    }

                    issueManagement {
                        system 'github'
                        url 'https://github.com/DirectMyFile/JPower/issues'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'kaendfinger'
                            name 'Kenneth Endfinger'
                            roles {
                                role 'developer'
                            }
                        }
                        developer {
                            id 'logangorence'
                            name 'Logan Gorence'
                            roles {
                                role 'developer'
                            }
                        }
                    }
                }
            }
        }
    }
}
